plugins {
    id 'java'
}

group 'de.mschramm.neverdie'
version '1.0.0'

task fatJar(type: Jar) {
  manifest.from jar.manifest
  classifier = 'all'
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  } {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
    duplicatesStrategy "exclude"
  }
  with jar
}

artifacts {
    archives fatJar
}

repositories {
    mavenCentral()
    maven { url "https://papermc.io/repo/repository/maven-public/" }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.34.0'
}

tasks.register('copyJarToPlugins', Copy) {
    from fatJar
    into './serverdata/plugins'
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

build.dependsOn copyJarToPlugins
